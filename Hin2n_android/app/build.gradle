apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
//          commandLine 'git', 'describe', '--tags', '--always', '--dirty', '--match', 'hin2n_v*'
            commandLine 'git', 'describe', '--tags', '--always', '--match', 'hin2n_v*'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getEnvOrConsole = { env, prompt ->
    def val = null
    try {
        val = System.getenv(env)
        if (val == null || val == "") {
            if (System.console() != null) {
                val = System.console().readLine("\n" + prompt + ": ")
            }
        }
    } catch (ignored) {
        return null
    }
    return val
}

def getManifestValues = { id_key, variant ->
    def val = true
    def id = getEnvOrConsole(id_key, id_key)
    if (id == null || id == "") {
        gradle.println(id_key + " is not set")
        val = false
    } else {
        if (variant) {
            variant.mergedFlavor.manifestPlaceholders.put(id_key + "_VALUE", id)
        }
    }

    return val
}

def getAllManifestValues = { variant ->
    def v1 = getManifestValues("UMENG_APPKEY", variant)
    def v2 = getManifestValues("BUGLY_APPID", variant)
    def v3 = getManifestValues("SHARE_WX_APPID", variant)
    def v4 = getManifestValues("SHARE_WX_APPSECRET", variant)
    def v5 = getManifestValues("SHARE_WB_APPID", variant)
    def v6 = getManifestValues("SHARE_WB_APPSECRET", variant)

    return v1 && v2
}


android {
    compileSdk 32
//    buildToolsVersion '30.0.3'

    namespace 'wang.switchy.hin2n'
    defaultConfig {
        applicationId "wang.switchy.hin2n"
        minSdk 17
        targetSdk 31
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled true
//        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "normal", ARCH: "all", UMENG_APPKEY_VALUE: "", BUGLY_APPID_VALUE: "",
//                                SHARE_WX_APPID_VALUE: "", SHARE_WX_APPSECRET_VALUE: "",
//                                SHARE_WB_APPID_VALUE: "", SHARE_WB_APPSECRET_VALUE: ""]
        resConfigs "en", "zh-rCN", "zh-rTW"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                cppFlags ""
                cFlags "-D__ANDROID_NDK__"
            }
        }
    }


    ndkVersion = '25.1.8937393'
    signingConfigs {
        debug {}

        release {
            keyAlias 'hin2n'
            keyPassword KEY_PASSWORD
            storeFile file('../signkey.jks')
            storePassword STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "LOG_DEBUG", "true")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        release {
            buildConfigField("boolean", "LOG_DEBUG", "false")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->

    }

//    gradle.taskGraph.beforeTask { Task task ->
//        if (task.name.contains("Release")) {
//            task.onlyIf {
//                return getAllManifestValues()
//            }
//        }
//    }


    flavorDimensions "channel", "arch"

    productFlavors {
        normal {
            dimension "channel"
            manifestPlaceholders += [UMENG_CHANNEL_VALUE: "normal"]
        }

        arm {
            dimension "arch"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
                manifestPlaceholders += [ARCH: "arm"]
            }
        }
        x86 {
            dimension "arch"
            ndk {
                abiFilters "x86", "x86_64"
                manifestPlaceholders += [ARCH: "x86"]
            }
        }
        allarch {
            dimension "arch"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
                manifestPlaceholders += [ARCH: "allarch"]
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

greendao {
    schemaVersion 4
    daoPackage 'wang.switchy.hin2n.storage.db.base'
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.8'
    implementation 'com.google.android.material:material:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'org.greenrobot:eventbus:latest.integration'

    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.baoyz.swipemenulistview:library:1.3.0'
    implementation 'com.wang.avi:library:2.1.3'
    implementation 'com.orhanobut:logger:1.15'
//    implementation 'com.yanzhenjie:permission:2.0.3'
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    implementation 'com.github.f0ris.sweetalert:library:latest.release'
}
